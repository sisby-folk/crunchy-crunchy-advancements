pluginManagement {
	repositories {
		maven {
			name = 'Quilt'
			url = 'https://maven.quiltmc.org/repository/release'
		}
		// Currently needed for Intermediary and other temporary dependencies
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		gradlePluginPortal()
	}
}

dependencyResolutionManagement {
	versionCatalogs {
		libs {
			def mc_major = '1.19'
			def mc = mc_major + '.2'
			def fapi = '0.76.0'

			// // Build Tools // //
			version('loom', '1.2.+')

			plugin('loom', 'org.quiltmc.loom').versionRef('loom')

			// // Base // //
			version('mc', mc)
			version('ql', "0.19.0")
			version('qm', "${mc}+build.22")
			version('fapi', "${fapi}-${mc}")
			version('qfapi', "4.0.0-beta.30+${fapi}-${mc}")
			version('nightConfig', "3.6.6")
			version('quiltConfig', "1.0.0-beta.6")

			library('mc', 'mojang', 'minecraft').versionRef('mc')
			library('ql', 'org.quiltmc', 'quilt-loader').versionRef('ql')
			library('qm', 'org.quiltmc', 'quilt-mappings').versionRef('qm')
			library('qfapi', 'org.quiltmc.quilted-fabric-api', 'quilted-fabric-api').versionRef('qfapi')
			library('nightConfig', 'com.electronwill.night-config', 'core').versionRef('nightConfig')
			library('nightConfigToml', 'com.electronwill.night-config', 'toml').versionRef('nightConfig')
			library('quiltConfig', 'org.quiltmc', 'quilt-config').versionRef('quiltConfig')
		}
	}
}
