//file:noinspection GroovyAccessibility
//file:noinspection GroovyAssignabilityCheck
buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		classpath 'com.modrinth.minotaur:Minotaur:2.4.3'
		classpath "com.github.breadmoirai:github-release:2.4.1"
	}
}

plugins {
	id 'maven-publish'
	alias libs.plugins.loom
}

if (System.getenv("MODRINTH_TOKEN")) {
	apply plugin: "com.modrinth.minotaur"
}
if (System.getenv("GITHUB_TOKEN")) {
	apply plugin: "com.github.breadmoirai.github-release"
}

archivesBaseName = project.archives_base_name

def mc_major = '1.19'
def mc = mc_major + '.2'
def default_branch = '1.18'

def origVersion = project.version
project.version = project.version+ "+" + mc_major
group = project.maven_group

repositories {
}

dependencies {
	// // Base // //
	minecraft libs.mc
	mappings variantOf(libs.qm) { classifier "intermediary-v2" }
	modImplementation libs.ql
	modImplementation libs.qfapi

	compileOnly libs.nightConfig
	compileOnly libs.nightConfigToml
	compileOnly libs.quiltConfig

	include libs.nightConfig
	include libs.nightConfigToml
	include libs.quiltConfig
}

processResources {
	final Map<String, String> map = [
		"version"        : version.toString(),
		"mc"             : libs.versions.mc.get(),
		"ql"             : libs.versions.ql.get(),
		"fapi"          : libs.versions.qfapi.get(),
		"qfapi"          : libs.versions.qfapi.get()
	]

	inputs.properties(map)
	filesMatching('*.mod.json') { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}


if (System.getenv("MODRINTH_TOKEN")) {
	modrinth {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = 'crunchy-crunchy-advancements'
		versionNumber = project.version
		versionName = origVersion
		versionType = 'release'
		uploadFile = remapJar
		gameVersions = [mc]
		loaders = ['quilt', 'fabric']
		detectLoaders = false
		dependencies {
		}
		changelog = "Changelog: https://github.com/sisby-folk/crunchy-crunchy-advancements/releases/tag/v"+origVersion
		syncBodyFrom = rootProject.file("README.md").text
	}
}

if (System.getenv("GITHUB_TOKEN")) {
	githubRelease {
		token System.getenv("GITHUB_TOKEN")
		owner "sisby-folk"
		repo "crunchy-crunchy-advancements"
		tagName 'v'+origVersion
		releaseName origVersion
		targetCommitish default_branch
		draft false
		prerelease false
		releaseAssets remapJar.archiveFile
		allowUploadToExisting true
		generateReleaseNotes true
	}
	tasks.githubRelease.dependsOn remapJar
}

if (System.getenv("MODRINTH_TOKEN") && System.getenv("GITHUB_TOKEN")) {
	tasks.register("fullRelease") {
		group "publishing"
		dependsOn "githubRelease"
		dependsOn "modrinth"
	}
}
